def projectId = 'multi-user-test-runner'
def scmOrganization = 'Vincit'
def signPackage = Boolean.parseBoolean(
        project.hasProperty("signPackage") ? signPackage : "true"
)

static String readFile(String filename) {
    return new File(filename).getText("UTF-8");
}

static String getVersion() {
    try {
        return readFile("version");
    } catch(IOException e) {
        try {
            return readFile("../version");
        } catch(IOException ex) {
            return "dev";
        }
    }
}

def mutrVersion = getVersion()
def snapshotPrefix = '-SNAPSHOT'

println("Using repositories:")
println("  $System.env.RELEASE_REPOSITORY_URL")
println("  $System.env.SNAPSHOT_REPOSITORY_URL")

def springVersion = System.env.SPRING_VERSION ?: '4.2.0.RELEASE'
def springSecurityVersion = System.env.SPRING_SECURITY_VERSION ?: '3.2.5.RELEASE'

task tagRelease << {
    version = mutrVersion ?: "test"

    if (System.env.SCM_USERNAME == null) {
        throw new java.lang.IllegalArgumentException("SCM_USERNAME must be defined")
    }
    if (System.env.SCM_PASSWORD == null) {
        throw new java.lang.IllegalArgumentException("SCM_PASSWORD must be defined")
    }
    exec {
        executable 'git'
        args 'tag', version
    }
    exec{
        executable 'git'
        args 'push', "https://$System.env.SCM_USERNAME:$System.env.SCM_PASSWORD@$System.env.REPO_URL", "refs/tags/$version"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    group = 'fi.vincit'
    sourceCompatibility = 1.8
    version = Boolean.parseBoolean("${System.env.SNAPSHOT?:true}") ? mutrVersion + snapshotPrefix : mutrVersion

    configurations {
        deployerJars
    }
    
    repositories {
        mavenCentral()
    }
    
    dependencies {
        deployerJars 'org.apache.maven.wagon:wagon-http-lightweight:3.3.3'
    }
    
    jar {
        manifest {
            attributes 'Implementation-Title': archivesBaseName,
                       'Implementation-Version': version
        }
    }
    
    publishing {
        repositories {
            maven {
                def releasesRepoUrl = "$System.env.RELEASE_REPOSITORY_URL"
                def snapshotsRepoUrl = "$System.env.SNAPSHOT_REPOSITORY_URL"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = "$System.env.DEPLOY_USERNAME"
                    password = "$System.env.DEPLOY_PASSWORD"
                }
            }
        }
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    if (signPackage) {
        gradle.taskGraph.whenReady { taskGraph ->
            if (taskGraph.allTasks.any { it instanceof Sign }) {
                allprojects { ext."signing.keyId" = "$System.env.SIGNING_KEY_ID" }
                allprojects { ext."signing.secretKeyRingFile" = "$System.env.SIGNING_KEY_RING_FILE" }
                allprojects { ext."signing.password" = "$System.env.SIGNING_PASSWORD" }
            }
        }
    }
    
    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
            ignoreFailures = true
            excludeFilter = file("$rootProject.projectDir/findBugsExcludeFilter.xml")
        }
    }
}


project(':core') {
    
    archivesBaseName = 'multi-user-test-runner'

    javadoc {
        failOnError = true
        options.overview = "src/main/java/overview.html"
    }

    dependencies {
        compileOnly group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        compileOnly group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
        compileOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.0'
        compileOnly group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.0.0'
    }
    
    publishing {
        publications {
            core(MavenPublication) {
                artifactId = 'multi-user-test-runner'
                from components.java
                artifact sourcesJar
                artifact javadocJar
                
                pom {
                    name = 'Multi User Test Runner'
                    description = '''
JUnit 4.12+ test runner for testing authentication with multiple users. Makes testing
authorization with multiple user/role combination easier and reduces amount of boilerplate
code.
'''
                    url = "https://${scmOrganization}.github.io/$projectId/".toString()
                    scm {
                        connection = "scm:git:git@github.com/$scmOrganization/${projectId}.git".toString()
                        developerConnection = "scm:git:git@github.com/$scmOrganization/${projectId}.git".toString()
                        url = "https://github.com/$scmOrganization/$projectId".toString()
                    }
                    inceptionYear = '2015-2019'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'juhasipo'
                            name = 'Juha Siponen'
                        }
                    }
                }
            }
        }
    }
    
    if (signPackage) {
        signing {
            sign publishing.publications.core
        }
    }
}

project(':integration-test') {
    dependencies {
        compile project(':core')

        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

project(':spring-test-class-runner') {
    archivesBaseName = 'multi-user-test-runner-spring'
    
    println("Using Spring Core version $springVersion")
    println("Using Spring Security version $springSecurityVersion")
    
    dependencies {
        compile project(':core')
        compile group: 'org.springframework', name: 'spring-test', version: springVersion
        compile group: 'org.springframework.security', name: 'spring-security-core', version: springSecurityVersion
        compileOnly group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        compileOnly group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
    
    publishing {
        publications {
            springRunner(MavenPublication) {
                artifactId = 'multi-user-test-runner-spring'
                from components.java
                artifact sourcesJar
                artifact javadocJar
                
                pom {
                    name = 'Multi User Test Runner Spring Runner'
                    description = 'Spring service testing support for Multi User Test Runner'
                    url = "https://${scmOrganization}.github.io/$projectId/".toString()
                    scm {
                        connection = "scm:git:git@github.com/$scmOrganization/${projectId}.git".toString()
                        developerConnection = "scm:git:git@github.com/$scmOrganization/${projectId}.git".toString()
                        url = "https://github.com/$scmOrganization/$projectId".toString()
                    }
                    inceptionYear = '2015-2019'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'juhasipo'
                            name = 'Juha Siponen'
                        }
                    }
                }
            }
        }
    }
    
    if (signPackage) {
        signing {
            sign publishing.publications.springRunner
        }
    }
    
}

project(':junit-5-legacy-test') {
    dependencies {
        compile project(':core')
        compile project(':integration-test')
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.0'
        testCompile group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '4.12.0'
    }

    test {
        useJUnitPlatform()
    }

    sourceCompatibility = 1.8
}

project(':junit-5-test') {
    dependencies {
        compile project(':core')
        compile project(':integration-test')
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.0.0'
        testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.0'
        testRuntime group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.0.0'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    }

    test {
        useJUnitPlatform()
    }

    sourceCompatibility = 1.8
}

project(':spring-test') {
    
    dependencies {
        compile project(':core')
        compile project(':integration-test')

        compile group: 'org.springframework.security', name: 'spring-security-core', version: springSecurityVersion
        compile group: 'org.springframework', name: 'spring-test', version: springVersion
        compile project(':spring-test-class-runner')

        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'junit', name: 'junit', version: '4.12'

    }
}

project(':examples') {

    sourceCompatibility = 1.8

    def springBootVersion = '2.0.0.RELEASE'
    def h2Version = '1.4.192'

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-security:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion")
        compile("com.h2database:h2:$h2Version")
        compile("org.apache.httpcomponents:httpclient:4.5.3")
        compile("javax.interceptor:javax.interceptor-api:1.2")
        testCompile project(':core')
        testCompile project(':spring-test-class-runner')
        testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile 'com.jayway.restassured:rest-assured:2.5.0'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.0'
        testCompile group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.0.0'
    }

    // From Petri Kainnulainen's examples
    // https://github.com/pkainulainen/gradle-examples/blob/master/integration-tests/build.gradle

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/test/java')
            }
        }
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
    }
    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }
    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}

project(':integration-test') {

    sourceCompatibility = 1.8

    def springBootVersion = '2.0.0.RELEASE'
    def h2Version = '1.4.192'

    dependencies {
        compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-security:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion")
        compile("com.h2database:h2:$h2Version")
        compile("org.apache.httpcomponents:httpclient:4.5.3")
        compile("javax.interceptor:javax.interceptor-api:1.2")
        testCompile project(':core')
        testCompile project(':spring-test-class-runner')
        testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile 'com.jayway.restassured:rest-assured:2.5.0'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.0'
        testCompile group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.0.0'
    }

    // From Petri Kainnulainen's examples
    // https://github.com/pkainulainen/gradle-examples/blob/master/integration-tests/build.gradle

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/test/java')
            }
        }
    }

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
    }
    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }
    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}
